use std
use sys

use "types"
use "repo"
use "report-html"

pkg contbuild =
	const run : (build : build# -> void)
;;

const run = {build
	for t in build.targs
		if !std.fexists(t._repo)
			std.chdir(build.scratch)
			clone(t)
		;;
	;;

	while true
		for t in build.targs
			std.chdir(t._repo)
			runonce(build, t)

		;;
		std.usleep(5*std.Sec)
	;;
}

const runonce = {build, targ
	var r : report
	var ranfix, ok

	r = [
		.build=[.cmd="", .out="", .err="", .ok=true],
		.test=[.cmd="", .out="", .err="", .ok=true],
		.fix=[.cmd="", .out="", .err="", .ok=true],
		.clean=[.cmd="", .out="", .err="", .ok=true],
	]

	for u in updates(targ)
		std.put("Current update: {}\n", u)
		ranfix = false
		reset(&r)
		if !checkout(targ, u)
			continue
		;;
:retry
		if !testrun(targ, &r)
			ok = false
			if !ranfix
				ranfix = true
				runcmd(targ.fix, &r.fix)
				goto retry
			;;
		else
			ok = true
		;;
		report(build, targ, &r, ok, u)
		purge(targ)
	;;
	checkout(targ, targ.tip)
}

const testrun = {t, r
	-> runcmd(t.build, &r.build) && \
		runcmd(t.test, &r.test) && \
		runcmd(t.clean, &r.clean)
}


const updates = {t
	var old, new, err, ret
	var changed = false

	err = ""
	old = [][:]
	new = [][:]
	ret = [][:]

	match head(t)
	| `std.Ok r:
		old = r
	| `std.Err e:	
		err = e
		goto error
	;;

	if !pull(t)
		goto error
	;;


	match head(t)
	| `std.Ok r:
		new = r
	| `std.Err e:	
		err = e
		goto error
	;;

	match between(t, old, new)
	| `std.Ok r:
		ret = r
		goto done
	| `std.Err e:
	;;
:error
	std.put("unable to get updates: {}\n", err)
:done
	std.slfree(err)
	std.slfree(old)
	std.slfree(new)
	-> ret
}

const report = {build, targ, report, status, hash
	var changed

	changed = targ._status != status

	/* update the book keeping */
	targ._status = status
	std.fput(build.logfd, "{} {} {}\n", hash, status, targ.targ)
	std.slpush(&targ._hist, (std.sldup(hash), status))

	reporthtml(build, targ, report, hash);
	/* annoying reports should only get fired if we change */
}

const reset = {r
	resetcmd(&r.build)
	resetcmd(&r.test)
	resetcmd(&r.fix)
	resetcmd(&r.clean)
}

const resetcmd = {c
	set(&c.out, "")
	set(&c.err, "")
	c.ok = true
}

const set = {s, v
	std.slfree(s#)
	s# = v
}

const runcmd = {cmd, r
	std.put("{}\n", cmd)
	r.ok = true
	if cmd.len == 0
		-> true
	else
		set(&r.cmd, std.strjoin(cmd, " "))
		match std.espork(cmd)
		| `std.Ok (pid, stdin, stdout, stderr):
			match std.fslurp(stdout)
			| `std.Ok o:	set(&r.out, o)
			| `std.Err e:	set(&r.out, std.fmt("could not read stdout: {}\n", e))
			;;
			match std.fslurp(stderr)
			| `std.Ok o:	set(&r.err, o)
			| `std.Err e:	set(&r.err, std.fmt("could not read stderr: {}\n", e))
			;;
			match std.wait(pid)
			| `std.Wsuccess:
			| _:	r.ok = false
			;;
		| `std.Err e:
			r.ok = false
			set(&r.err, std.fmt("could not run command {}\n", cmd))
		;;
		-> r.ok
	;;
}

